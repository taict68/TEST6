//Power by Nguyễn Văn Chick
//@version=5

indicator("Chạc Đà 2.0", overlay= false, max_bars_back = 5000, max_labels_count = 500, max_lines_count = 500)

//input

tc             = input.bool(false, " Nhập tay hong bro? ", group = "Chạc đà")

highestvol_    = input(0.000,'Max Vol( Đơn vị M)', group = "Chạc đà")*1000000
highestspread_ = input(0.000,'Max Spread', group = "Chạc đà")

gapluon     = input.bool ( true, 'Gap không bro?', group = "Chạc đà")
colorchacda = input.color(color.aqua,' Đà màu gì', group = "Chạc đà")

scale = input.string('Normal', ' Chạc đà on Scale?', options = ['Normal', 'Log', 'Percent'], group = "Chạc đà")

// Calculations
float diff = na

if gapluon
    if barstate.isfirst 
        diff := switch scale
            "Log"     => diff := math.log(close) - math.log(open)  
            "Percent" =>  diff := ( close - open)/ open   
            => diff := close - open
    else 
        diff := switch scale
            "Log"     => diff := math.log(close) - math.log(close[1])  
            "Percent" =>  diff := ( close - close[1])/ open   
            => diff := close - close[1]   
else
    diff := switch scale
        "Log"     => diff := math.log(close) - math.log(open)  
        "Percent" =>  diff := ( close - open)/ open   
        => diff := close - open


absdiff = math.abs(diff)


//highest Vol & Spread

float highestspread = na 
float highestvol    = na 

float maxvol = 0.000
float maxspread = 0.000

maxvol := ta.max(nz(volume))
maxspread := ta.max( absdiff)

if tc 

    highestspread := highestspread_  
    highestvol    := highestvol_ 

else 
    highestspread := maxspread[1]
    highestvol    := maxvol[1]

spreadscore = highestspread / 5
volumescore = highestvol / 5

float total = na
float diemvol = na
float diemspread = na 

diemvol    :=  volume / volumescore
diemspread := absdiff / spreadscore

total := diemspread + diemvol

// T/G

int dir = 0 

ascdiff = absdiff >= absdiff[1]
desdiff = absdiff <= absdiff[1]
equaldiff = absdiff == absdiff[1]

xanhxanh = diff >= 0 and diff[1] >= 0
dodo     = diff <= 0 and diff[1] <= 0

xanhdo   = diff <= 0 and diff[1] >= 0
doxanh   = diff >= 0 and diff[1] <= 0

if xanhxanh and ascdiff
    dir := 1
else if xanhxanh and desdiff
    dir := -1
else if xanhdo and ascdiff
    dir := -1
else if xanhdo and desdiff
    dir := -1
else if xanhxanh and equaldiff
    if volume > volume[1]
        dir:= -1
    else
        dir := 1
else if dodo and ascdiff
    dir := -1 // 'G'
else if dodo and desdiff
    dir := 1 // 'T'
else if doxanh and ascdiff
    dir := 1 // 'T'
else if doxanh and desdiff
    dir := 1 // 'T'
else if dodo and equaldiff
    if volume > volume[1]
        dir := 1 // 'T'
    else
        dir := -1 // 'G'

float o = 0
float c = 0

if ( dir == 1 and dir[1] == 1 ) or (dir == 1 and dir[1] == -1 )
    o := nz(c[1])
    c := o + total

else if ( dir == -1 and dir[1] == 1) or (dir == -1 and dir[1] == -1 )
    o := nz(c[1]) 
    c := o - total

plotcandle( o, o>c? o : c, o>c? c: o, c, ' Chạc đà', color = colorchacda, editable = false)

ATH = input.bool(false,'Cây đà to quá!', group = "Chạc đà")

verticalLn = ATH? diemvol > 5 or diemspread >5 ? line.new(bar_index, 0, bar_index, 0, extend = extend.both, color=color.black) : na : na

// table

statPos   = input.string('Bottom Right', '', options=['Top Left', 'Top Center', 'Top Right', 'Middle Right', 'Bottom Left', 'Bottom Center','Bottom Right'], inline='STAT', group="Table") 
i_textSize3   = input.string("Normal", "  Table Text Size", options = [ "Tiny", "Small", "Normal"], inline='STAT', group="Table")
textSize3     = i_textSize3 == "Small" ? size.small : i_textSize3 == "Normal" ? size.normal : size.tiny

statPosition = switch statPos
    'Top Left'      => position.top_left
    'Top Center'    => position.top_center
    'Top Right'     => position.top_right
    'Middle Right'  => position.middle_right
    'Bottom Left'   => position.bottom_left
    'Bottom Center' => position.bottom_center
    'Bottom Right'  => position.bottom_right

var table momtable = table.new(statPosition, 3, 3, border_width = 3)

if barstate.isconfirmed
    table.cell(momtable, 0, 0, "",  text_color = color.black, bgcolor = color.new(color.black, 75 ), text_halign = text.align_left, text_size = textSize3)
    table.cell(momtable, 1, 0, "Số sao ghi zậy nhen",  text_color = color.black, bgcolor = color.new(color.black, 75), text_halign = text.align_left, text_size = textSize3)

    table.cell(momtable, 0, 1, "Max Vol( M)" ,  text_color = color.black, bgcolor = color.new(color.black, 75), text_halign = text.align_left, text_size = textSize3)
    table.cell(momtable, 1, 1, str.tostring(maxvol/1000000) ,  text_color = color.black, bgcolor = color.new(color.black, 100), text_halign = text.align_left, text_size = textSize3)

    table.cell(momtable, 0, 2, "Max Spread",  text_color = color.black, bgcolor = color.new(color.black, 75), text_halign = text.align_left, text_size = textSize3)
    table.cell(momtable, 1, 2, str.tostring(maxspread)      ,  text_color = color.black, bgcolor = color.new(color.black, 100), text_halign = text.align_left, text_size = textSize3)

// bar number 


demkhong  = input.bool(false, 'Cần thầy đếm nến phụ hong?' , group = " Bar number chôm của thầy")
plotPrice = input.float(0.00,title="Plot Price Level:", group = " Bar number chôm của thầy")

lnClr = input.color(color.new(color.fuchsia,50),title = "Line Color:", group = " Bar number chôm của thầy")
lblClr = input.color(color.new(color.purple,0),title = "Number Color:", group = " Bar number chôm của thầy")

var int numberCounter = 0
var line verticalLn1 = na
var label numberLbl = na
string numberVal = ""

//Time Range Check and Plot Line/Numbers
numberCounter := numberCounter+1 
numberStr = str.tostring(numberCounter)

length = str.length(numberStr)
for i = 0 to length
    numberVal := numberVal + str.substring(numberStr,i,i+1)+"\n"
    
getLength = str.length(numberVal)
numberVal := str.substring(numberVal,0,getLength-2)

verticalLn1 := demkhong? line.new(bar_index, plotPrice, bar_index, plotPrice, extend = extend.both, color=lnClr, width = 1) : na
numberLbl := demkhong? label.new(bar_index,plotPrice,text = numberVal, textcolor=lblClr, style = label.style_none) :na
